{
  parserClass="kz.pompei.lanact.gen.language.ActParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Act"
  psiImplClassSuffix="Impl"
  psiPackage="kz.pompei.lanact.gen.language.psi"
  psiImplPackage="kz.pompei.lanact.gen.language.impl"

  elementTypeHolderClass="kz.pompei.lanact.gen.language.ActTokenTypes"
  elementTypeClass="kz.pompei.lanact.parser.ActElementType"
  tokenTypeClass="kz.pompei.lanact.parser.ActTokenType"


}

actFile ::= COMMENT* CLASS id DO statements DONE

id ::= WORD (WORD|NUMBER)*

statements ::= statement*

statement ::= statement_expr | statement_if | statement_do_done

statement_do_done ::= DO statements DONE

statement_expr ::= LET expression assign_part?

assign_part ::= ASSIGN expression

statement_if ::= IF expression statements statement_else_if* statement_else? END IF

expression ::= id | expression_const | PAR_OPEN expression PAR_CLOSE | expression_sq | expression_str

expression_str ::= STR_LEFT expression (STR_INNER expression)* STR_RIGHT

expression_sq ::= SQ_OPEN expression SQ_CLOSE

expression_const ::= NUMBER | YES | NO | STR_CONST

statement_else_if ::= ELSIF expression statements

statement_else ::= ELSE statements
